task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.compile
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.env.BINTRAY_USER ?: getConfig('bintrayUser')
    key = System.env.BINTRAY_API_KEY ?: getConfig('bintrayApiKey')

    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'mobile-engage-sdk'
        desc = 'Mobile Engage SDK'
        websiteUrl = 'https://github.com/emartech/android-mobile-engage-sdk'
        vcsUrl = 'https://github.com/emartech/android-mobile-engage-sdk.git'
        licenses = ['MPL-2.0']
        publish = true
        publicDownloadNumbers = true
    }
}

def getConfig(key) {
    if (project.hasProperty(key)) {
        return project."${key}"
    } else {
        return null
    }
}